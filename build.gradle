apply from: 'dependencies.gradle'
apply plugin: 'java-library'

repositories {
    maven { url "http://jars.interlis.ch" }
    maven { url "https://repo.osgeo.org/repository/release/" }
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
}    

configurations {
    api {
        exclude module: 'xmlparserv2'
        exclude module: 'ucp'
        exclude module: 'xdb6'
        exclude module: 'oraclepki'
        exclude module: 'osdt_core'
        exclude module: 'osdt_cert'
    }  
}

configurations.all {
    resolutionStrategy { 
        force 'ch.interlis:iox-ili:1.20.15'
        force 'ch.interlis:ili2c-tool:5.0.2'
        force 'ch.interlis:ili2c-core:5.0.2'
        force 'ch.interlis:ili2pg:4.3.2'
        force 'ch.interlis:ili2gpkg:4.3.2'
    }
}

dependencies {
    // See original build.gradle in the gretl repo for more
    // information.
    api 'ch.so.agi:gretl:2.1.+'

    // Add 3rd party libs which we want in the docker image.
    api 'de.undercouch:gradle-download-task:3.4.1'
    api 'org.hidetake:gradle-ssh-plugin:2.9.0'
    api 'jp.classmethod.aws:gradle-aws-plugin:0.41'

    // Add ilivalidator custom functions
    // ...
    // See original build.gradle in the gretl repo for more
    // information.
    
    api oracleDependency
}

task stageJars(type: Copy) {
    description = 'Copies all jars required by gretl to the temp folder __jars4image.'
    from configurations.compileClasspath
    from configurations.runtimeClasspath
    into "gretl/__jars4image"
}
